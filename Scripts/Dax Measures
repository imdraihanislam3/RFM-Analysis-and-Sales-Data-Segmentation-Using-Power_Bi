#DAX USED


#KPI’s:
Total_Customers = DISTINCTCOUNT('Superstore 2025'[Customer ID])
Total_sales = SUM('Superstore 2025'[Sales])
AvgRecency = AVERAGE(RFM_Calculation[Recency])
AvgFrequency = AVERAGE(RFM_Calculation[Frequency])
AvgMonetary = AVERAGE(RFM_Calculation[Monetary])

#DAX for Creating Date Table
DateTable = 
ADDCOLUMNS(
    CALENDAR(DATE(2021,1,1),DATE(2025,12,31)),
    "Year",YEAR([DATE]),
    "Month Number",MONTH([Date]),
    "Month Name", FORMAT([Date],"MMMM"),
    "Year-Month", FORMAT([Date], "YYYY-MM"),
    "Quarter", "Q" & FORMAT([Date],"Q"),
    "Day",DAY([Date]),
    "Week Number", WEEKNUM([Date],2),
    "Day of week", WEEKDAY([Date],2),    --1 = SUNDAY Start 2= MONDAY Start
    "Day Name", FORMAT([Date], "dddd")
    )




#DAX for Creating RFM Calculation Table

RFM_Calculation =  
 SUMMARIZE(
    'Superstore 2025',
    'Superstore 2025'[Customer ID],
    'Superstore 2025'[Customer Name],
    "Recency", DATEDIFF(
        CALCULATE(MAX('Superstore 2025'[Order Date])), 
        CALCULATE(MAX('Superstore 2025'[Order Date]), ALL('Superstore 2025')), DAY),
    "Frequency", DISTINCTCOUNT('Superstore 2025'[Order ID]),
    "Monetary", SUM('Superstore 2025'[Sales]))

#DAX Calc Column within RFM Calculation Table

#Frequency_Score :
Frequency_Score = 
var Rankfrq=
RANKX(
    ALL(RFM_Calculation),
    RFM_Calculation[Frequency],,DESC)

var Totalcust=COUNTROWS(ALL(RFM_Calculation))
RETURN
    CEILING(DIVIDE(Rankfrq * 5, Totalcust),1)

#Monetary_Score :
Monetary_Score = 
var Rankmon=
RANKX(
    ALL(RFM_Calculation),
    RFM_Calculation[Monetary],,DESC)

var Totalcust=COUNTROWS(ALL(RFM_Calculation))
RETURN
    CEILING(DIVIDE(Rankmon * 5, Totalcust),1)

#Recency_Score :
 Recency_Score = 
var Rankrecency=
RANKX(
    ALL(RFM_Calculation),
    RFM_Calculation[Recency],,ASC)

var Totalcust=COUNTROWS(ALL(RFM_Calculation))
RETURN
    CEILING(DIVIDE(Rankrecency * 5, Totalcust),1)

#Customer_Segmentation :
Customer_Segment = 
    Switch  (
        TRUE(),
    -- Champions: best across recency, frequency, and monetary
    RFM_Calculation[Recency_Score] <= 2 &&
    RFM_Calculation[Frequency_Score] <= 2 &&
    RFM_Calculation[Monetary_Score] <= 2, "Champions",

    -- Loyal Customers: frequent buyers, fairly recent
    RFM_Calculation[Frequency_Score] <= 2 &&
    RFM_Calculation[Recency_Score] <= 3, "Loyal Customers",

    -- Big Spenders: high monetary value, but not recent
    RFM_Calculation[Monetary_Score] <= 2 &&
    RFM_Calculation[Recency_Score] >= 3, "Big Spenders",

    -- At Risk: were good before, but haven’t purchased recently
    RFM_Calculation[Recency_Score] >= 3 &&
    RFM_Calculation[Frequency_Score] <= 3, "At Risk",

    -- Lost: weak in all 3 metrics
    RFM_Calculation[Recency_Score] = 5 &&
    RFM_Calculation[Frequency_Score] = 5 &&
    RFM_Calculation[Monetary_Score] = 5, "Lost",

    -- Default bucket
    "Others"
)

#YOY Variance:

YoY SalesVariance = 
 VAR LY = CALCULATE([Total_sales], SAMEPERIODLASTYEAR(Date_Table[Date]))
 VAR LYFormatted = FORMAT(LY/1000, "$#,0") & "K"
 VAR yoyCalc = DIVIDE([Total_sales] - LY, LY)
 VAR yoyCalcFormatted = FORMAT(yoyCalc, "0.0%; (0.0%)")

 RETURN
  SWITCH(
    TRUE(),
    yoyCalc > 0, UNICHAR(9650) & " " & yoyCalcFormatted & " | LY " & LYFormatted,
    ISBLANK(LY), UNICHAR(8211) & " | No Data for Last Year",
    yoyCalc = 0, UNICHAR(8211) & " | No Change from Last Year",
    UNICHAR(9660) & " " & yoyCalcFormatted & " | LY " & LYFormatted)


#YoY FrequencyVar :
YoY FrequencyVar = 
 VAR LY = CALCULATE([AvgFrequency], SAMEPERIODLASTYEAR(Date_Table[Date]))
 VAR LYFormatted = FORMAT(LY, "#,0")
 var yoyCalc = DIVIDE(([AvgFrequency] - LY) , LY)
 var yoyCalcFormatted = FORMAT(yoyCalc,"0.0%;(0.0%)")
 
RETURN
 SWITCH(
    True(),
    yoyCalc >0, UNICHAR(9650) & " " & yoyCalcFormatted & " | LY " & LYFormatted,
    ISBLANK(LY), UNICHAR(8211) & " | No Data for Last Year ",
    

    UNICHAR(9660) & " " & yoyCalcFormatted  & " | LY " & LYFormatted
 )

#YoY MonetaryVar : 
 YoY MonetaryVar = 
 VAR LY = CALCULATE([AvgMonetary], SAMEPERIODLASTYEAR(Date_Table[Date]))
 VAR LYFormatted = FORMAT(LY/1000, "$#,0.00") & "K"
 var yoyCalc = DIVIDE(([AvgMonetary] - LY) , LY)
 var yoyCalcFormatted = FORMAT(yoyCalc,"0.0%;(0.0%)")
 
RETURN
 SWITCH(
    True(),
    yoyCalc >0, UNICHAR(9650) & " " & yoyCalcFormatted & " | LY " & LYFormatted,
    ISBLANK(LY), UNICHAR(8211) & " | No Data for Last Year ",
    

    UNICHAR(9660) & " " & yoyCalcFormatted  & " | LY " & LYFormatted
 )


#YoY RecencyVar = 
YoY RecencyVar = 
 VAR LY = CALCULATE([AvgRecency], SAMEPERIODLASTYEAR(Date_Table[Date]))
 VAR LYFormatted = FORMAT(LY, "#,0")
 var yoyCalc = DIVIDE(([AvgRecency] - LY) , LY)
 var yoyCalcFormatted = FORMAT(yoyCalc,"0.0%;(0.0%)")
 
RETURN
 SWITCH(
    True(),
    yoyCalc >0, UNICHAR(9650) & " " & yoyCalcFormatted & " | LY " & LYFormatted,
    ISBLANK(LY), UNICHAR(8211) & " | No Data for Last Year ",
    

    UNICHAR(9660) & " " & yoyCalcFormatted  & " | LY " & LYFormatted
 )




